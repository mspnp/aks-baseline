// This file is embedded into the Bicep file.
// Source: https://learn.microsoft.com/azure/azure-monitor/insights/container-insights-alerts

let endDateTime = now();
let startDateTime = ago(1h);
let trendBinSize = 1m;
let clusterName = "${clusterName}";

KubePodInventory 
| where TimeGenerated < endDateTime 
| where TimeGenerated >= startDateTime 
| where ClusterName == clusterName
| distinct ClusterName, TimeGenerated
| summarize ClusterSnapshotCount = count() by bin(TimeGenerated, trendBinSize), ClusterName
| join hint.strategy=broadcast (
    KubePodInventory
    | where TimeGenerated < endDateTime
    | where TimeGenerated >= startDateTime
    | distinct ClusterName, Computer, PodUid, TimeGenerated, PodStatus
    | summarize TotalCount = count(), 
                PendingCount = sumif(1, PodStatus =~ "Pending"),
                RunningCount = sumif(1, PodStatus =~ "Running"),
                SucceededCount = sumif(1, PodStatus =~ "Succeeded"),
                FailedCount = sumif(1, PodStatus =~ "Failed") by ClusterName,
                bin(TimeGenerated, trendBinSize)
    ) on ClusterName, TimeGenerated
| extend UnknownCount = TotalCount - PendingCount - RunningCount - SucceededCount - FailedCount
| project TimeGenerated,
          TotalCount = todouble(TotalCount) / ClusterSnapshotCount,
          PendingCount = todouble(PendingCount) / ClusterSnapshotCount,
          RunningCount = todouble(RunningCount) / ClusterSnapshotCount,
          SucceededCount = todouble(SucceededCount) / ClusterSnapshotCount,
          FailedCount = todouble(FailedCount) / ClusterSnapshotCount, UnknownCount = todouble(UnknownCount) / ClusterSnapshotCount
| summarize AggregatedValue = avg(FailedCount) by bin(TimeGenerated, trendBinSize)
